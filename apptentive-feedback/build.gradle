apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        consumerProguardFiles 'consumer-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        gradle.startParameter.taskNames.each {
            if (it.contains("AndroidTest")) {
                minSdkVersion 26 // we need this workaround in order to fix "com.android.tools.r8.a: MethodHandle.invoke and MethodHandle.invokeExact are only supported starting with Android O (--min-api 26)"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

//    Android tests with mockk wont run without this
    testOptions {
        packagingOptions {
            resources.excludes.add("META-INF/LICENSE.md")
            resources.excludes.add("META-INF/LICENSE-notice.md")
        }
    }
    namespace 'apptentive.com.android.feedback'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':apptentive-core')
    implementation "androidx.appcompat:appcompat:$appcompat_library_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.exifinterface:exifinterface:$exifinterface"

    testImplementation project(':apptentive-core-test')
    testImplementation project(':apptentive-feedback-test')
    testImplementation "androidx.test:core:$androidx_test_version"
    testImplementation "com.google.truth:truth:$truth_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "org.json:json:$json_version"

    androidTestImplementation project(':apptentive-core-test')
    androidTestImplementation project(':apptentive-feedback-test')
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
    androidTestImplementation "io.mockk:mockk-agent:$mockk_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_version"
    androidTestImplementation "androidx.test:core:$androidx_test_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
}

project.ext {
    groupId = 'com.apptentive'
    artifactId = 'apptentive-feedback'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                //'-Xexplicit-api=warning', // or '-Xexplicit-api=strict'
                '-opt-in=apptentive.com.android.util.InternalUseOnly',
        ]
    }
}

apply from: '../publish.gradle'
